#!/usr/bin/env coffee

async = require 'async'
Mongolian = require 'mongolian'
config = require '../../config'
commands = require '../../commands'
queries = require '../../queries'
data = require '../../data'
db = require '../../db'
settlement = null
character_names = ['Ecce', 'Deschain', 'TroyMG', 'Romani']
characters = {}
tiles = [
  {x: -1, y: -1, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: 0, y: -1, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: 1, y: -1, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: -1, y: 0, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: 0, y: 0, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: 1, y: 0, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: -1, y: 1, terrain: data.terrains.forest_2, region: data.regions.midlands}
  {x: 0, y: 1, terrain: data.terrains.grassland, region: data.regions.midlands}
  {x: 1, y: 1, terrain: data.terrains.hill_low_side, region: data.regions.midlands}
]
buildings = [
  {x: -1, y: 0, building: data.buildings.hut}
  {x: 0, y: -1, building: data.buildings.hut}
  {x: 1, y: 0, building: data.buildings.hut}
]
items = [
  {to: 'Ecce', item: data.items.axe_hand}
  {to: 'Ecce', item: data.items.log}
  {to: 'Ecce', item: data.items.test_weapon}
  {to: 'Ecce', item: data.items.poultice}
  {to: 'Ecce', item: data.items.onion}
]
xp = 1000
move =
  Ecce: {x: 1, y: 0, z: 0}
  TroyMG: {x: 1, y: 0, z: 0}
  Romani: {x: 1, y: 0, z: 0}
  Deschain: {x: 1, y: 0, z: 1}
founder = 'TroyMG'
hq = x: 0, y: 0, z: 0
settlement_name = 'Test Settlement'

async.series [
  (cb) ->
    # DROP DATABASE!!!
    _db = new Mongolian config.mongo_uri
    _db.dropDatabase (err) ->
      return cb(err) if err?
      console.log "Database dropped."
      cb()
  , (cb) ->
    async.forEachSeries tiles, (t, cb) ->
      return cb("invalid terrain") unless t.terrain?
      commands.create_tile {x: t.x, y: t.y, z: 0}, t.terrain.id, t.region.id, (err, tile) ->
        return cb(err) if err?
        console.log "Created #{tile.terrain} tile at #{tile.x},#{tile.y},#{tile.z}."
        cb()
    , cb
  , (cb) ->
    async.forEachSeries character_names, (name, cb) ->
      commands.create_character name, "#{name.toLowerCase()}@example.com", 'password', settlement, (err, c) ->
        return cb(err) if err?
        console.log "Created character #{c.name}."
        characters[c.name] = c
        cb()
    , (err) ->
      return cb(err) if err?
      query =
        name: character_names[0]
      update =
        $set:
          developer: true
      db.characters.update query, update, cb
  , (cb) ->
    async.forEachSeries buildings, (b, cb) ->
      queries.get_tile_by_coords {x: b.x, y: b.y, z: 0}, (err, tile) ->
        return cb(err) if err?
        commands.create_building tile, b.building, (err) ->
          return cb(err) if err?
          console.log "Built a #{b.building.name} at #{tile.x},#{tile.y},#{tile.z}."
          cb()
    , cb
  , (cb) ->
    async.forEachSeries items, (i, cb) ->
      character = characters[i.to]
      count = i.count ? 1
      commands.add_item character, i.item, count, (err) ->
        return cb(err) if err?
        if count is 1
          console.log "Gave a #{i.item.name} to #{character.name}."
        else
          console.log "Gave #{count}x #{i.item.plural} to #{character.name}."
        cb()
    , cb
  , (cb) ->
    arr = []
    arr.push c for key, c of characters
    async.forEachSeries arr, (c, cb) ->
      commands.xp c, xp, xp, xp, xp, (err) ->
        return cb(err) if err?
        console.log "Granted 60xp to each xp type for #{c.name}."
        cb()
    , cb
  , (cb) ->
    arr = []
    arr.push character: character, coords: coords for character, coords of move
    async.forEach arr, (a, cb) ->
      commands.teleport characters[a.character], null, a.coords, (err) ->
        return cb(err) if err?
        console.log "Moved #{a.character} to #{a.coords.x},#{a.coords.y},#{a.coords.z}."
        cb()
    , cb
  , (cb) ->
    queries.get_tile_by_coords hq, (err, hq) ->
      return cb(err) if err?
      commands.create_settlement characters[founder], hq, settlement_name, (err) ->
        return cb(err) if err?
        console.log "#{founder} founded the settlement of #{settlement_name}."
        cb()
], (err) ->
  if err?
    console.log err
  else
    console.log 'Bootstrap complete.'
  process.exit()
