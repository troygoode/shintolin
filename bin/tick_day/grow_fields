#!/usr/bin/env coffee

_ = require 'underscore'
async = require 'async'
db = require '../../db'
commands = require '../../commands'

async.series [
  (cb) ->
    query =
      building: 'field'
      hp:
        $gte: 5
        $lt: 200
    db.tiles.find(query).toArray (err, tiles) ->
      return cb(err) if err?
      async.each tiles, (tile, cb) ->
        growth = tile.hp - 5
        growth = (growth * 3.5) + 3
        growth = (growth * 2) if tile.watered
        growth = 200 - tile.hp if tile.hp + growth > 200
        query =
          _id: tile._id
        update =
          $inc:
            hp: growth
        db.tiles.update query, update, cb
      , cb
  (cb) ->
    query =
      overuse:
        $gt: 0
    update =
      $inc:
        overuse: -.5
    db.tiles.update query, update, false, true, cb
  (cb) ->
    update =
      $unset:
        watered: true
    db.tiles.update {}, update, false, true, cb
], (err) ->
  if err?
    console.log err
    process.exit 1
  else
    console.log 'Fields Grown'
    process.exit 0
